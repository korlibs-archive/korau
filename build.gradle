buildscript {
    ext.kloggerVersion = '0.1.0'
    ext.korioVersion = '0.17.1'
    ext.kormaVersion = '0.17.1'
    ext.kotlinVersion = '1.2.0-rc-39'
    ext.compilerVersion = ext.kotlinVersion
    ext.librariesVersion = ext.kotlinVersion
    ext.eapChannel = 'http://dl.bintray.com/kotlin/kotlin-eap-1.2'

    repositories {
        mavenLocal()
        jcenter()
        maven { url eapChannel }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$compilerVersion"
    }
}

allprojects {
    group 'com.soywiz'
    version "0.17.3-SNAPSHOT"

    if (project == rootProject) {
        new File(rootProject.rootDir, "korau/common/src/main/kotlin/com/soywiz/korau/KorauVersion.kt").write(
                "package com.soywiz.korau\n\nval KORAU_VERSION = \"$version\""
        )
    }

    apply plugin: 'java'
    //apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    //sourceCompatibility = 1.7
    //targetCompatibility = 1.7

    //kotlin { experimental { coroutines 'enable' } }

    configurations {
        provided
    }

    repositories {
        mavenLocal()
        maven { url eapChannel }
        mavenCentral()
    }

    it.afterEvaluate {
        if (it.plugins.hasPlugin("kotlin-platform-common")) {
            dependencies {
                compile "org.jetbrains.kotlin:kotlin-stdlib-common:$librariesVersion"
                testCompile "org.jetbrains.kotlin:kotlin-test-common:$librariesVersion"
                testCompile "org.jetbrains.kotlin:kotlin-test-annotations-common:$librariesVersion"
            }

            kotlin {
                experimental { coroutines 'enable' }
            }
        }
        if (it.plugins.hasPlugin("kotlin-platform-jvm") || it.plugins.hasPlugin("kotlin")) {
            dependencies {
                compile "org.jetbrains.kotlin:kotlin-stdlib:$librariesVersion"
                testCompile "org.jetbrains.kotlin:kotlin-test:$librariesVersion"
                testCompile "org.jetbrains.kotlin:kotlin-test-junit:$librariesVersion"
                testCompile "junit:junit:4.12"
            }

            kotlin {
                experimental { coroutines 'enable' }
            }

            compileJava.options.encoding = 'UTF-8'
            compileTestJava.options.encoding = 'UTF-8'

            sourceCompatibility = 1.7
            targetCompatibility = 1.7
        }
        if (it.plugins.hasPlugin("kotlin-platform-js") || it.plugins.hasPlugin("kotlin2js")) {
            dependencies {
                compile "org.jetbrains.kotlin:kotlin-stdlib-js:$librariesVersion"
                testCompile "org.jetbrains.kotlin:kotlin-test-js:$librariesVersion"
                testCompile "org.jetbrains.kotlin:kotlin-test-js:$librariesVersion"
            }

            kotlin {
                experimental { coroutines 'enable' }
            }

            compileKotlin2Js {
                kotlinOptions.moduleKind = "umd"
                kotlinOptions.sourceMap = true
            }

            compileTestKotlin2Js {
                kotlinOptions.moduleKind = "umd"
                kotlinOptions.sourceMap = true
            }
        }
    }

    task javadoc2(type: Javadoc) {
        failOnError = false
    }

    task javadocJar(type: Jar, dependsOn: javadoc2) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

// gradle uploadArchives
    if (project.hasProperty('sonatypeUsername')) {
        signing {
            sign configurations.archives
        }

        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: project.sonatypeUsername, password: project.sonatypePassword)
                    }

                    pom.project {
                        name "${project.name}"
                        packaging 'jar'
                        description 'Korau: Kotlin cORoutines AUdio'
                        url 'https://github.com/soywiz/korau/'
                        inceptionYear '2017'

                        scm {
                            url 'scm:git@github.com:soywiz/korau.git'
                            connection 'scm:git@github.com:soywiz/korau.git'
                            developerConnection 'scm:git@github.com:soywiz/korau.git'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'soywiz'
                                name 'Carlos Ballesteros Velasco'
                            }
                        }
                    }
                }
            }
        }
    }

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                groupId project.group
                artifactId project.name
                version "$project.version"
            }
        }
    }

    task deploy(dependsOn: ['install', 'uploadArchives']) {
    }
}